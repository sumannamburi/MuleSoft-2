%dw 1.0
%output application/java

%var directorIDs = ["JKLS483S","FJSA48JD","NMA4FJ9K"]

//Combines Accounts and Transactions by the Account ID. Assigns each account to a
//director
%function consolidateAccountsTrans (payload)
  payload.accounts map ( (account, index) ->
  using (id = account.id as :string)
  (
    account ++
    {
      transactions: payload.transactions filter ($.customerRef == id)
    } ++
    {
      assignedDirector: directorIDs[index mod sizeOf directorIDs]
    }
  )
)
---
using (data = consolidateAccountsTrans(payload))
(data groupBy $.assignedDirector)
